#!/bin/tcsh
#$ -S /bin/tcsh
source ./code/code.main/custom-tcshrc    # customize shell environment

##
## USAGE: scripts-submit-jobs JOBS-SCRIPT [MAX-JOBS]
##

if ($#argv < 1) then
  grep '^##' $0
  exit
endif

set jobs_script = $1
set max_jobs = $2
if ($max_jobs == "") set max_jobs = `cat $jobs_script | wc -l`


if (`cat $jobs_script | wc -l` > 0) then
  set script_dir = `readlink -f $jobs_script | sed 's/\/[^/]\+$//'`
  cat $jobs_script | sed 's/$/ ; sleep 1/' >! $script_dir/__run-with-wait
  set pref = $script_dir/__run_
  rm -f ${pref}*
  split -a 10 -l $max_jobs $script_dir/__run-with-wait $pref
  foreach run (${pref}*)
    scripts-send2err "Running $run script..."
    chmod +x $run
    set jid = `$run`
    scripts-send2err "Waiting for all jobs [$jid] to complete..."
    scripts-qsub-wait "$jid"
  end
  rm -f ${pref}*
endif

scripts-send2err "Done."




#!/bin/tcsh
#$ -S /bin/tcsh
source ./code/code.main/custom-tcshrc     # customized shell environment

##
## USAGE: run [--dry-run]
##

set dry_run = $1

# install required R packages
source inputs/params/params.tcsh
Rscript ./code/install-hicbench-r-packages.r

# validate sample sheet
scripts-send2err "Validating sample sheet..."
./code/validate-sample-sheet.tcsh inputs/sample-sheet.tsv
if ($status != 0) then
  scripts-send2err "Errors found in sample sheet, aborting..."
  exit 1
endif

# run pipeline
scripts-send2err "PIPELINE STARTING: `date`"
foreach d (__*)
  set p = `pwd`
  
  # enter directory and run
  cd $d
  if (! -e run-custom) then
    ./run $dry_run
  else 
    ./run-custom $dry_run
  endif
  
  if ($dry_run == "") then
    # check for errors
    scripts-send2err "Checking job log files for errors..."
    ./code/code.main/pipeline-errors results
    if (`cat errors/error.log | wc -l` > 0) then
      scripts-send2err "Errors encountered, aborting!"
      exit 1
    endif
  endif
  
  # go back to main directory
  cd $p
end
scripts-send2err "PIPELINE COMPLETED: `date`"


